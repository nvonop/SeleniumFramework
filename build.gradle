apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'groovy'

group = 'org.nvonop.selenium.framework'
version = '1.2.6'

repositories {
	// Spock releases are available from Maven Central
	mavenCentral()
	// Spock snapshots are available from the Sonatype OSS snapshot repository
	maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
	compile "org.codehaus.groovy:groovy-all:2.1.5"
	testCompile "org.spockframework:spock-core:1.0-groovy-2.0-SNAPSHOT"
	compile "org.seleniumhq.selenium:selenium-java:2.+"
	compile group: 'com.google.inject', name: 'guice', version: '3.0'
	compile("org.uncommons:reportng:1.1.2") {
		exclude group: "org.testng", module: "testng"
	}

}

test {
	systemProperties = System.getProperties()
	maxParallelForks = 3
	systemProperties['BUILD'] = version.toString()
	systemProperties['LOCAL_DRIVER'] = false
	systemProperties['REMOTE_DRIVER'] = false
	systemProperties['SAUCE_DRIVER'] = true
	systemProperties['SAUCE_KEY'] = 'http://'+System.getenv('SAUCE_USERNAME')+':'+System.getenv('SAUCE_ACCESS_KEY')+'@ondemand.saucelabs.com:80/wd/hub'
	systemProperties['GRID_HOST'] = 'localhost'
	systemProperties['GRID_PORT'] = '4444'
	systemProperties['BROWSER'] = System.getenv('BROWSER')
	systemProperties['VERSION'] = System.getenv('VERSION')
	systemProperties['PLATFORM'] = ''
	systemProperties['TIMEOUT'] = '10'
	systemProperties['TEST_RESULTS_PATH'] = './build/Screenshots'
	systemProperties['APPLICATION_URL'] = "http://selenium-framework.site44.com/controls_page.html"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

jar { exclude('src/org/nvonop/selenium/test') }


uploadArchives {
	repositories {
		mavenDeployer {
			repository (
					url: 'https://api.bintray.com/maven/nvonop/maven/SeleniumFramework'
					) {
						authentication(
								userName: System.getenv('BINTRAY_USERNAME'),
								password: System.getenv('BINTRAY_ACCESS_KEY')
								)
					}
		}
	}
}

